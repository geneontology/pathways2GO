https://www.slideshare.net/goodb/clipboards/knowledge-graph-nots

in rdf.geneontology.org

select ?s_label ?o_label  
where { 
  ?s <http://purl.obolibrary.org/obo/RO_0002411> ?o .
  ?o rdf:type ?o_term . 
  ?o_term rdfs:label ?o_label . 
  ?s rdf:type ?s_term . 
  ?s_term rdfs:label ?s_label . 
  filter(?o_label = "chemotaxis") 
  # filter(?o_label = "Ras palmitoyltransferase activity") 
}
limit 10



PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX bd: <http://www.bigdata.com/rdf#>

prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix obo: <http://purl.obolibrary.org/obo/>
prefix biopax: <http://www.biopax.org/release/biopax-level3.owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>
        
        PREFIX BP: <http://purl.obolibrary.org/obo/GO_0008150>
        PREFIX MF: <http://purl.obolibrary.org/obo/GO_0003674>
        PREFIX CC: <http://purl.obolibrary.org/obo/GO_0005575>
	   # PREFIX causally_upstream_of: <http://purl.obolibrary.org/obo/RO_0002411>
		 # retrieve pathways in the active graph

SELECT ?mf1 ?bp_local ?bp_class ?uniprot_uri 
#?prot_uris
#?biological_process
where { 
  ?mf1 <http://purl.obolibrary.org/obo/RO_0002411> ?mf2 . # causally linked functions
  ?mf1 <http://purl.obolibrary.org/obo/BFO_0000050> ?bp_local . # part of the same process
  ?mf2 <http://purl.obolibrary.org/obo/BFO_0000050> ?bp_local . 
  ?bp_local rdf:type ?bp_class .  
#  filter (?bp_class != <http://purl.obolibrary.org/obo/GO_0008150>) . 
  filter (?bp_class != owl:NamedIndividual) .
  filter (?bp_class != <http://purl.obolibrary.org/obo/BFO_0000015>) . 
  filter (?bp_class != <http://purl.obolibrary.org/obo/BFO_0000003>) . 
   ?mf1 rdfs:label ?mf1_label .
   ?mf2 rdfs:label ?mf2_label . 
#   filter(?mf2_label = "Dissociation of clathrin-associated proteins") #filter for specific downstream function
  ?mf1 <http://purl.obolibrary.org/obo/RO_0002333> ?protein_complex_instance . #protein that enabled the upstream function
  ?protein_complex_instance rdf:type <http://purl.obolibrary.org/obo/GO_0032991> . 
  ?protein_complex_instance <http://purl.obolibrary.org/obo/BFO_0000051> ?protein_instance . 
  ?protein_instance rdf:type ?uniprot_uri . 
  filter (?uniprot_uri != owl:NamedIndividual) .
  filter (?uniprot_uri != <http://purl.obolibrary.org/obo/PR_000000001>) . 
   FILTER NOT EXISTS {
 	?protein_instance <http://arachne.geneontology.org/indirect_type> ?uniprot_uri . 
 } 
#  SERVICE <http://identifiers.org/services/sparql> {
#    ?uniprot_uri owl:sameAs  ?prot_uris .
       #    <http://identifiers.org/uniprot/P09497> owl:sameAs  ?prot_uris .
       # FILTER(regex(str(?prot_uris), "purl.uniprot.org/uniprot" ))
#    }
              
#   SERVICE <https://query.wikidata.org/bigdata/namespace/wdq/sparql/> {
#     ?biological_process wdt:P2888 ?bp_class .  
#     ?wikidata_protein wdt:P682 ?biological_process . #biological processes (from the GO) linked to that protein 
#     ?wikidata_protein wdt:P2888 ?uniprot_uri 
# }
}

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bd: <http://www.bigdata.com/rdf#>

#Find drugs that are already on the market for some disease, 
#use paths created by gene-disease associations and gene ontology annotations 
#to identify new diseases where the drugs might be active
SELECT ?protein ?drugLabel ?diseaseLabel ?geneLabel ?biological_process ?biological_processLabel ?newgeneLabel ?newdiseaseLabel WHERE {
  ?disease wdt:P2176 ?drug . #disease treated by a drug
  ?gene wdt:P2293 ?disease .  #gene genetically associated with that disease 
  ?gene wdt:P703 wd:Q15978631 . # limit to human genes
  ?gene wdt:P688 ?protein .	  #protein encoded by that gene
  ?protein wdt:P682 ?biological_process . #biological processes (from the GO) linked to that protein 
  ?newprotein wdt:P682 ?biological_process . #a different protein linked to the same biological process
  ?newprotein wdt:P702 ?newgene .  #the gene that encodes the new protein
  ?newgene wdt:P703 wd:Q15978631 .
  ?newgene wdt:P2293 ?newdisease . #new diseases linked to the other gene (that might be addressed by the drug)
  #make sure its new (no looping back...)
  FILTER (?disease != ?newdisease) . 
  #get labels
  ?disease rdfs:label ?diseaseLabel .
  FILTER (LANG(?diseaseLabel) = "en") .  
  ?gene rdfs:label ?geneLabel .
  FILTER (LANG(?geneLabel) = "en") .
  ?newdisease rdfs:label ?newdiseaseLabel .
  FILTER (LANG(?newdiseaseLabel) = "en") .
  ?newgene rdfs:label ?newgeneLabel .
  FILTER (LANG(?newgeneLabel) = "en") .
  ?biological_process rdfs:label ?biological_processLabel .
  FILTER (LANG(?biological_processLabel) = "en") .
  ?drug rdfs:label ?drugLabel .
  FILTER (LANG(?drugLabel)="en")
}
limit 100

#cases where a drug physically interacts with the product of gene known to be genetically associated a disease
#these cases may show opportunities to repurpose a drug for a new disease
#See http://database.oxfordjournals.org/content/2016/baw083.long  and
#http://drug-repurposing.nationwidechildrens.org/search
#an example that was recently validated involved a new link between Metformin wd:Q19484 and cancer survival 

SELECT ?drug ?drugLabel ?gene ?geneLabel ?disease ?diseaseLabel WHERE {
  ?drug wdt:P129 ?gene_product .   # drug interacts with a gene_product 
  ?gene_product wdt:P702 ?gene .  # gene_product is encoded by a gene
  ?gene	wdt:P2293 ?disease .    # gene is genetically associated with a disease 
  # add labels
  	SERVICE wikibase:label {
        bd:serviceParam wikibase:language "en" .
	}
}
limit 1000

