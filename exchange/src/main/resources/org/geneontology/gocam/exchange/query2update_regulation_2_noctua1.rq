prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix obo: <http://purl.obolibrary.org/obo/>
prefix biopax: <http://www.biopax.org/release/biopax-level3.owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

select distinct ?reaction2 ?reaction1 ?reaction2enablerLabel ?reaction2Label ?reaction1Label ?pathway 
where { 
  # limit to relationships in the same Pathway
  # promiscuous molecules make this important to avoid explosions
  ?pathway obo:BFO_0000051 ?reaction1 .
  ?pathway obo:BFO_0000051 ?reaction2 . 
  ?reaction2 obo:RO_0002333 ?reaction2enabler . #enabled by                      
  ?reaction1 obo:RO_0002233 ?inputA . #has input
  ?reaction1 obo:RO_0002233 ?inputB . #has input 
  ?reaction1 obo:RO_0002234 ?r1output . #has output (some complex made from a and b)   
  ?inputA skos:exactMatch ?A . 
  ?reaction2enabler skos:exactMatch ?A .
  filter(?inputA != ?inputB) . 
  filter(?reaction1 != ?reaction2) .
  #don't match when the output of reaction1 (the putative negative regulator) 
  #"does anything" = "is the input or enabler for another reaction"
  optional { 
   ?reaction2enabler rdfs:label ?reaction2enablerLabel .
   ?reaction2 rdfs:label ?reaction2Label .
   ?reaction1 rdfs:label ?reaction1Label .
   ?reaction3 obo:RO_0002233|obo:RO_0002333 ?r3_input .
   ?r3_input skos:exactMatch ?C .
   ?r1output skos:exactMatch ?C .
   }
  FILTER (!BOUND(?reaction3)) . 
 } 

#TEST with Reactome:Signaling by BMP 
#https://reactome.org/content/detail/R-HSA-201451 

# RO_0002024  directly positively regulated by 
# RO_0002023  directly negatively regulated by 
# RO_0002334 regulated by
# obo:RO_0002233 has input 
# obo:RO_0002333 enabled by
# RO_0002234 has output
# obo:BFO_0000051 has part
# obo:RO_0002413 provides direct input for

