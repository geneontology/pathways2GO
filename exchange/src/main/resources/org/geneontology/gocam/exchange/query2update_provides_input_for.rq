prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix obo: <http://purl.obolibrary.org/obo/>
prefix biopax: <http://www.biopax.org/release/biopax-level3.owl#>
prefix skos: <http://www.w3.org/2004/02/skos/core#>

#If reaction1 has_output E 
#and reaction2 has_input E 
#and reaction1 causally_upstream_of reaction2 
#then reaction1 directly_positively_regulates reaction2

select distinct ?reaction2 ?reaction1 ?reaction2inputLabel ?reaction2Label ?reaction1Label ?pathway ?r1output ?reaction2input
where { 
  # limit to relationships in the same Pathway
  # promiscuous molecules make this important to avoid explosions
 ?reaction1 obo:BFO_0000050 ?pathway  .
 ?reaction2 obo:BFO_0000050 ?pathway  . 
  ?reaction1 obo:RO_0002411 ?reaction2 . 
  ?reaction2 obo:RO_0002233 ?reaction2input .                  
  ?reaction1 obo:RO_0002234 ?r1output .   
  ?r1output skos:exactMatch ?A . 
  ?reaction2input skos:exactMatch ?A .
  filter(?reaction1 != ?reaction2) .
	optional {
    ?reaction2input rdfs:label ?reaction2inputLabel .
    ?reaction2 rdfs:label ?reaction2Label .
    ?reaction1 rdfs:label ?reaction1Label .
    }
 } 

#TEST with Reactome:Signaling by BMP 
#https://reactome.org/content/detail/R-HSA-201451 

# RO_0002024  directly positively regulated by 
# RO_0002023  directly negatively regulated by 
# RO_0002334 regulated by
# obo:RO_0002233 has input 
# obo:RO_0002333 enabled by
# RO_0002234 has output
# obo:BFO_0000051 has part
# obo:RO_0002413 provides direct input for
# obo:RO_0002411 causally upstream of
