prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
prefix obo: <http://purl.obolibrary.org/obo/>
PREFIX metago: <http://model.geneontology.org/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
    
#what genes are in the pathway? 
# (count(distinct ?uniprot) as ?c)
select distinct ?uniprot where{ 
 # GRAPH <http://model.geneontology.org/275881076> {

  #Signaling by BMP obo:GO_0030509
  #Wnt Signaling GO:0016055 
  ?pathway rdf:type obo:GO_0008150 . 
  ?pathway rdfs:label ?pathwaylabel .
  filter(?pathwaylabel = "TCF dependent signaling in response to WNT") . 
  #get the sub pathways via has part if there are any 
  #note that this ends up including ?pathway = ?subpath (its a part of itself) 
  ?pathway obo:BFO_0000051* ?subpath .
  #check that its a pathway (biological process)
  ?subpath rdf:type obo:GO_0008150 .   
  #get the reactions in the pathways and subpathways via has part - 
  ?subpath obo:BFO_0000051 ?reaction . 
   { 
    ?uniprot rdfs:subClassOf obo:CHEBI_36080 . # = protein
    ?physical_entity rdf:type ?uniprot . 
    {?reaction ?rprop ?physical_entity}
     union
     {?physical_entity ?rprop ?reaction }
    ?reaction rdf:type obo:GO_0003674 . #molecular function
    }
    UNION
    {
      ?uniprot rdfs:subClassOf obo:CHEBI_36080 .
      ?physical_entity rdf:type ?uniprot . 
    #get parts recursively with * (not currently necessary as complexes have been collapsed and using reasoned kb anyway) 
      ?aggregate_entity obo:BFO_0000051 ?physical_entity .
    # and protein parts of complexes <http://purl.obolibrary.org/obo/GO_0032991>
      ?aggregate_entity rdf:type obo:GO_0032991 . 
          {?reaction ?rprop ?aggregate_entity}
     union
     {?aggregate_entity ?rprop ?reaction }
    #values ?rprop {obo:RO_0002327 obo:RO_0002428 obo:RO_0002430 obo:RO_0002429} .  
    }
    ?reaction rdfs:label ?reactionlabel . 
 #   filter(?reactionlabel = 'WNT binds to FZD and LRP5/6') 
#}
}
#GROUP BY ?reactionlabel 
#ORDER BY desc(?reactionlabel)
#ORDER BY ?subpathlabel ?reactionlabel 

  
